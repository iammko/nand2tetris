// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c, lastC;

        let c = 0;
        let lastC = 0;
        
        // 显示光标 '|'
        do Output.printChar(124);

        // 退出条件(c < 1 && lastC > 0),狄摩根定律取反
        while(c > 0 | lastC < 1)
        {
            let lastC = c;
            let c = Keyboard.keyPressed();
        }

        // 删除光标
        do Output.backSpace();
        // 打印字符
        do Output.printChar(lastC);

        return lastC;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String line;
        var char c;

        let line = String.new(80);

        do Output.printString(message);

        // 80个字符满了自动返回
        while(line.length() < 81)
        {
            let c = Keyboard.readChar();
            if (c = String.newLine())
            {
                // 换行符
                return line;
            }

            if(c = String.backSpace())
            {
                // 退格符
                do line.eraseLastChar();
            }
            else
            {
                do line.appendChar(c);
            }
        }

        return line;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String intStr;
        var int number;

        let intStr = Keyboard.readLine(message);
        let number = intStr.intValue();

        // 注意要释放内存
        do intStr.dispose();

        return number;
    }
}
